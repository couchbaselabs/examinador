#!/usr/bin/env groovy

/**
 * Copyright (C) Couchbase, Inc 2020 - All Rights Reserved
 * Unauthorized copying of this file, via any medium is strictly prohibited
 * Proprietary and confidential.
 *
 * When updating this Jenkinsfile, changes will not take effect immediately; they will take effect once the Jenkins
 * multi-branch pipeline picks up the commit. This therefore means that changes made to the Jenkinsfile in a Gerrit
 * review will not have any effect until they are submitted.
 */

import hudson.model.Result
import hudson.model.Run
import jenkins.model.CauseOfInterruption.UserInterruption

pipeline {
    agent { label "linux&&master&&large" }

    environment {
        SOURCE = "${WORKSPACE}/source"
        EXAMINADOR = "${WORKSPACE}/examinador"
    }

    stages {
        stage("Setup") {
            steps {
                script {
                    // Configure Gerrit Trigger
                    properties([pipelineTriggers([
                        gerrit(
                            serverName: "review.couchbase.org",
                            gerritProjects: [
                                [
                                    compareType: "PLAIN", disableStrictForbiddenFileVerification: false, pattern: "examinador",
                                    branches: [[ compareType: "PLAIN", pattern: "master" ]]
                                ],
                            ],
                            triggerOnEvents: [
                                commentAddedContains(commentAddedCommentContains: "reverify"),
                                draftPublished(),
                                patchsetCreated(excludeNoCodeChange: true)
                            ]
                        )
                    ])])
                }

                slackSend(
                    channel: "#tooling-cv",
                    color: "good",
                    message: "Build for '<${GERRIT_CHANGE_URL}|${GERRIT_CHANGE_SUBJECT}>' by '${GERRIT_CHANGE_OWNER_NAME}' started (${env.BUILD_URL})"
                )

                timeout(time: 10, unit: "MINUTES") {
                    // Install linter
                    timeout(time: 10, unit: "MINUTES") {
                        sh "pip3 install --user urllib3 pylint requests mypy==0.730"
                    }

                    // Initialise the build directory
                    dir("${WORKSPACE}") {
                        sh "git clone git@github.com:couchbaselabs/examinador.git"
                    }

                    // Fetch the commit we are testing
                    dir("${EXAMINADOR}") {
                        sh "git fetch ssh://buildbot@review.couchbase.org:29418/examinador ${GERRIT_REFSPEC}"
                        sh "git checkout FETCH_HEAD"
                    }
                }
            }
        }

        stage("Setup Examinador") {
            steps {
                dir("${EXAMINADOR}") {
                    // Use python virtual environment to make life easier
                    sh '''#!/bin/bash
                          python3 -m venv robot-env
                          source robot-env/bin/activate
                          pip3 install wheel
                          pip3 install -r requirements.txt --no-use-pep517 
                          pip3 install pylint mypy==0.730
                    '''

                    //Install chromedriver
                    sh '''#!/bin/bash
                          source robot-env/bin/activate
                          pip3 install webdrivermanager
                          webdrivermanager chrome --linkpath ${EXAMINADOR}/robot-env/bin
                    '''
                  }
            }
        }

        stage("Lint") {
            steps {
                timeout(time: 10, unit: "MINUTES") {
                    dir("${EXAMINADOR}") {
                        sh '''#!/bin/bash
                              source robot-env/bin/activate
                              python3 -m pylint --rcfile=${EXAMINADOR}/.pylintrc libraries
                        '''
                    }
                }
            }
        }

        stage("Type check") {
            steps {
                timeout(time: 10, unit: "MINUTES") {
                    dir("${EXAMINADOR}") {
                        sh """#!/bin/bash
                            source robot-env/bin/activate
                            if [ \$(mypy --ignore-missing-imports libraries | grep -c error) -gt 1 ]; then
                                echo "Failed mypy type checking in libraries"
                                echo "Re running: mypy --ignore-missing-imports libraries"
                                echo \$(mypy --ignore-missing-imports libraries)
                                exit 1
                            fi
                           """
                    }
                }
            }
        }

        stage("Get CB") {
            steps {
                timeout(time: 15, unit: "MINUTES") {
                    dir("${SOURCE}") {
                        sh "repo init -u git://github.com/couchbase/manifest -m branch-master.xml -g all"
                        sh "repo sync -j8"
                    }
                }
            }
        }

        stage("Build CB") {
            steps {
                timeout(time: 60, unit: "MINUTES") {
                    dir("${SOURCE}") {
                        sh "make -j8"
                    }
                }
            }
        }

        stage("Test") {
            steps{
                dir("${EXAMINADOR}") {
                    // Run test suites
                    sh '''#!/bin/bash
                          source robot-env/bin/activate
                          ulimit -n 9000
                          localstack start > /dev/null 2>&1 &
                          robot-env/bin/robot --variable SOURCE:${SOURCE} --variable WORKSPACE:${WORKSPACE} --variable DELETE_LOGS:True --outputdir ${WORKSPACE}/reports --exclude in_progress --exclude wait_for_bug_fix --exclude UI --consolewidth 120 -L DEBUG *_tests rest_api_longer
                    '''
                }
            }
      }

    }

    post {
        always {
            // Post the test results
            script {
              step(
                    [
                      $class              : 'RobotPublisher',
                      outputPath          : 'reports',
                      outputFileName      : 'output.xml',
                      reportFileName      : 'report.html',
                      logFileName         : 'log.html',
                      otherFiles          : '*.zip',
                      disableArchiveOutput: false,
                      passThreshold       : 100,
                      unstableThreshold   : 95,
                    ]
                )
            }
        }

        success {
            submitGerritVerifyStatus(1)
        }

        unstable {
            submitGerritVerifyStatus(-1)
        }

        failure {
            submitGerritVerifyStatus(-1)
        }

        aborted {
            submitGerritVerifyStatus(-1)
        }

        cleanup {
            // Remove the workspace
            deleteDir()
        }
    }
}

/**
 * Taken from:
 * https://github.com/couchbase/server-cv/blob/a345959cfa6546819b4c03520d0d016bc16c4370/jenkins-jobs/Jenkinsfile
 *
 * Report vote to Gerrit verify-status plugin.
 *
 * Adds the job result to the verify-status sidebar in the Gerrit patch
 * view. This is hacky - the Jenkins plugin that does this for normal jobs
 * does not appear to have been updated to be used from Pipelines.
 * If a way of using the proper plugin is found, or it is updated, it should
 * definitely replace this.
 */
def submitGerritVerifyStatus(value) {
    if (env.GERRIT_PATCHSET_REVISION == null) {
        return
    }
    // Directly report the verify status to Gerrit
    // The jenkins plugin which reports to the verify-status sidebar does not seem to be up to date
    // TODO: Investigate the HTTP API for greater portability (e.g., windows!)
    def url = "http://cv.jenkins.couchbase.com/job/Examinador%20cv%20multi-branch/job/master/${BUILD_NUMBER}/"

    sh """ssh -p ${env.GERRIT_PORT} buildbot@${env.GERRIT_HOST} \
    -o "UserKnownHostsFile=/dev/null" -o "StrictHostKeyChecking=no" \
    verify-status save --verification \
    "'name=Examinador cv multi-branch pipeline|value=${value}|url=${url}|reporter=buildbot'" \
    ${GERRIT_PATCHSET_REVISION}"""
}
